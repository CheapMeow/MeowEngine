#version 450

struct Particle {
	vec2 position;
	vec2 velocity;
    vec4 color;
};

layout (binding = 0) uniform ParameterUBO {
    float deltaTime;
} ubo;

layout(std140, binding = 1) readonly buffer ParticleSSBOIn {
   Particle particles[ ];
} inParticles;

layout(std140, binding = 2) buffer ParticleSSBOOut {
   Particle particles[ ];
} outParticles;

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

void main() 
{
    uint index = gl_GlobalInvocationID.x;  

    Particle particleIn = inParticles.particles[index];

    outParticles.particles[index].position = particleIn.position + particleIn.velocity.xy * ubo.deltaTime;
    outParticles.particles[index].velocity = particleIn.velocity;

    // Flip movement at window border
    if ((outParticles.particles[index].position.x <= -1.0) || (outParticles.particles[index].position.x >= 1.0)) {
        outParticles.particles[index].velocity.x = -outParticles.particles[index].velocity.x;
    }
    if ((outParticles.particles[index].position.y <= -1.0) || (outParticles.particles[index].position.y >= 1.0)) {
        outParticles.particles[index].velocity.y = -outParticles.particles[index].velocity.y;
    }

}